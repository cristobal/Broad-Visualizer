<?xml version="1.0" encoding="utf-8"?>
<s:Group xmlns:fx="http://ns.adobe.com/mxml/2009" 
		 xmlns:s="library://ns.adobe.com/flex/spark" 
		 xmlns:mx="library://ns.adobe.com/flex/mx" 
		 xmlns:menus="com.broad.ui.menus.*" 
		 xmlns:view="com.broad.view.*" 
		 xmlns:node="com.broad.ui.windows.node.*"
		 xmlns:topology="com.broad.ui.windows.topology.*" 
		 xmlns:sequences="com.broad.ui.windows.sequences.*"
		 top="0" right="0" bottom="0" left="0"
		 creationComplete="initComponent(event)">
	<fx:Script>
		<![CDATA[
			import com.broad.core.events.TimedEvent;
			import com.broad.ui.menus.ProgressTimeSlider;
			import com.broad.ui.windows.BaseWindow;
			import com.broad.ui.windows.charts.ChartsWindow;
			import com.broad.ui.windows.charts.ChartsWindows;
			import com.broad.ui.windows.node.NodeWindows;
			import com.broad.ui.windows.sequences.SequencesWindow;
			import com.broad.ui.windows.topology.TopologyWindow;
			import com.broad.ui.windows.topology.TopologyWindows;
			
			import flash.utils.getTimer;
			import flash.utils.setTimeout;
			
			import mx.events.CloseEvent;
			import mx.events.FlexEvent;
			import mx.states.SetStyle;
			
			import spark.components.Application;
			import spark.effects.Animate;
			import spark.effects.animation.MotionPath;
			import spark.effects.animation.SimpleMotionPath;
			
			/**
			 * ApplicationWindow.mxml
			 * 
			 * @author Cristobal Dabed
			 */			
			
			//--------------------------------------------------------------------------
			//
			//  Initialization
			//
			//--------------------------------------------------------------------------
			
			/**
			 * Init component
			 * 
			 * @param event
			 */ 
			private function initComponent(event:FlexEvent):void
			{
				initComponents();
				bindComponents();
			}
			
			/**
			 * Init components
			 */ 
			private function initComponents():void
			{
				_chartsWindows   = new ChartsWindows(this);
				_nodeWindows     = new NodeWindows(this);
				_topologyWindows = new TopologyWindows(this);
				
				// setup sequences window
				_sequencesWindow = new SequencesWindow();
				_sequencesWindow.setInitialPosition("", 10, 400, "");
				_sequencesWindow.visible = false;
				addElement(_sequencesWindow);
			}
			
			/**
			 * Bind components
			 */ 
			private function bindComponents():void
			{
				addEventListener(Event.ADDED_TO_STAGE, handleAddedToStage);
				
				// playback events
				playback.addEventListener(ProgressTimeSlider.CHANGE_START, handleProgressSliderChangeStart);
				playback.addEventListener(ProgressTimeSlider.CHANGE_END, handleProgressSliderChangeEnd);
				playback.addEventListener(ProgressTimeSlider.LOAD_START, handleProgressSliderLoadStart);
				playback.addEventListener(ProgressTimeSlider.LOAD_END, handleProgressSliderLoadEnd);
				
				// menu events
				menu.addEventListener(Menu.STATS_CLICK, handleMenuStatsButtonClick);
				menu.addEventListener(Menu.SEQUENCES_CLICK, handleMenuSequencesButtonClick);
				menu.addEventListener(Menu.LOCAL_TOPOLOGY_CLICK, handleMenuLocalTopologyButtonClick);
				menu.addEventListener(Menu.TOPOLOGY_CLICK, handleMenuTopologyButtonClick);
			}
			
			
			//--------------------------------------------------------------------------
			//
			//  Variables
			//
			//--------------------------------------------------------------------------
			
			/**
			 * @private
			 */ 
			private var duration:uint;
			
			/**
			 * @private
			 */ 
			private var loaded:Number;
			
			/**
			 * @private
			 */ 
			private var lastLoadedInvalidation:Number;
			
			/**
			 * @private
			 */ 	
			private var resize:Boolean;

			/**
			 * @private
			 */ 
			private var _chartsWindows:ChartsWindows;
			
			/**
			 * @readonly chartsWindows
			 */ 
			public function get chartsWindows():ChartsWindows
			{
				return _chartsWindows;
			}
			
			/**
			 * @private
			 */ 
			private var _nodeWindows:NodeWindows;
			
			/**
			 * @readonly nodeWindows
			 */ 
			public function get nodeWindows():NodeWindows
			{
				return _nodeWindows;
			}
			
			/**
			 * @private
			 */ 
			private var _sequencesWindow:SequencesWindow;
			
			/**
			 * @readonly sequencesWindow
			 */ 
			public function get sequencesWindow():SequencesWindow
			{
				return _sequencesWindow;
			}
			
			/**
			 * @private
			 */ 
			private var _topologyWindows:TopologyWindows;
			
			/**
			 * @readonly topologyWindows
			 */ 
			public function get topologyWindows():TopologyWindows
			{
				return _topologyWindows;
			}
			
			/**
			 * @private
			 */ 
			private var _isDesktop:Boolean;
			
			/**
			 * @readwrite isDesktop
			 */ 
			public function get isDesktop():Boolean
			{
				return _isDesktop;
			}
			
			public function set isDesktop(value:Boolean):void
			{
				_isDesktop = value;
			}
			
			
			//--------------------------------------------------------------------------
			//
			//  Methods
			//
			//--------------------------------------------------------------------------
			
			/**
			 * Set time
			 * 
			 * @param time
			 */ 
			public function setTime(time:uint):void
			{
				_nodeWindows.setTime(time);
				playback.setTime(time);
				topologyWindows.setTime(time);
				sequencesWindow.setTime(time);
				chartsWindows.setTime(time);
			}
			
			/**
			 * Set duration
			 */ 
			public function setDuration(duration:uint):void
			{
				playback.setDuration(duration);
				chartsWindows.setDuration(duration);
			}
			
			/**
			 * Set loaded
			 */ 
			public function setLoaded(value:Number):void
			{
				playback.setLoaded(value);
				loaded = value;
				
				value = value - (value % 10);
				if (value != lastLoadedInvalidation) {
					lastLoadedInvalidation = value;
					chartsWindows.invalidateWindows(); // while is still loading invalidate windows
				}
			}
			
			public function reset():void
			{
				visualizerView.loaderViewVisible = true;
				sequencesWindow.visible = false;
				
				setDuration(0);
				topologyWindows.reset();
				nodeWindows.reset();
				chartsWindows.reset();
				menu.reset();
				playback.reset();
				
				setPlaybackEnabled(false);
				setTimeout(function():void {
					visualizerView.loaderViewVisible = false;
				}, 2500);
			}
			
			/**
			 * Set playback enabled
			 * 
			 * @param value
			 */ 
			public function setPlaybackEnabled(value:Boolean):void
			{
				playback.menu.enabled = value;
			}
			
			/**
			 * Set sequences enabled
			 * 
			 * @param value
			 */ 
			public function setStatsEnabled(value:Boolean):void
			{
				menu.setStatsEnabled(value);
			}
			
			/**
			 * Set sequences enabled
			 * 
			 * @param value
			 */ 
			public function setSequencesEnabled(value:Boolean):void
			{
				menu.setSequencesEnabled(value);
			}
			
			/**
			 * Set topology enabled
			 * 
			 * @param value
			 */ 
			public function setTopologyEnabled(value:Boolean):void
			{
				menu.setTopologyEnabled(value);
			}
			
			/**
			 * Set local topolgy enabled
			 * 
			 * @param value
			 */ 
			public function setLocalTopologyEnabled(value:Boolean):void
			{
				menu.setLocalTopologyEnabled(value);
			}
			
			/**
			 * Set perimeter view enabled
			 * 
			 * @param value
			 */ 
			public function setPerimeterViewEnabled(value:Boolean):void
			{
				playback.setPerimeterViewEnabled(value);
			}
			
			
			/**
			 * @override
			 */ 
			override public function invalidateSize():void
			{
				if (resize){
					if (width != parent.width) {
						width = parent.width;
					}
				
					if (height != parent.height) {
						height = parent.height;
					}
				}
				
				super.invalidateSize();
			}
			
			/**
			 * Handle resize
			 * 
			 * @param event
			 */ 
			private function handleResize(event:Event):void
			{
				resize = true;
				invalidateSize();
				resize = false;
			}
			
			
			//--------------------------------------------------------------------------
			//
			//  Playback progress events
			//
			//--------------------------------------------------------------------------
			
			/**
			 * Handle progress slider change start
			 * 
			 * @param event
			 */ 
			private function handleProgressSliderChangeStart(event:Event):void
			{
				
			}
			
			/**
			 * Handle progress slider change end
			 * 
			 * @param event
			 */ 
			private function handleProgressSliderChangeEnd(event:Event):void
			{
				visualizerView.loaderViewVisible = playback.buffering;
			}
			
			/**
			 * Handle progress slider loading start
			 * 
			 * @param event
			 */ 
			private function handleProgressSliderLoadStart(event:Event):void
			{
				visualizerView.loaderViewVisible = true;
			}
			
			/**
			 * Handle progress slider loading end
			 * 
			 * @param event
			 */ 
			private function handleProgressSliderLoadEnd(event:Event):void
			{
				visualizerView.loaderViewVisible = false;
			}	
			
			/**
			 * Handle added to stage
			 * 
			 * @param event
			 */ 
			private function handleAddedToStage(event:Event):void
			{
				removeEventListener(Event.ADDED_TO_STAGE, handleAddedToStage);
				stage.addEventListener(Event.RESIZE, handleResize);
				stage.addEventListener(FullScreenEvent.FULL_SCREEN, handleFullScreenEvent);
			}
			
			//--------------------------------------------------------------------------
			//
			//  FullScreen functionality
			//
			//--------------------------------------------------------------------------
			
			/**
			 * @private
			 */ 
			private var fullScreenTimer:Timer;
			
			/**
			 * @private
			 */ 
			private var fullScreenState:Boolean;
			
			/**
			 * @private
			 */ 
			private var fullScreenAnimate:Animate;
			
			/**
			 * @private
			 */ 
			private var normalScreenRect:Rectangle;
			
			/**
			 * @private
			 */ 
			private var windows:Vector.<BaseWindow>;
			
			/**
			 * Trace handle fullscreen event
			 * 
			 * @param event
			 */ 
			private function handleFullScreenEvent(event:FullScreenEvent):void
			{
				fullScreenState = stage.displayState == StageDisplayState.FULL_SCREEN || stage.displayState == StageDisplayState.FULL_SCREEN_INTERACTIVE;
				if (fullScreenState) {
					systemManager.getSandboxRoot().addEventListener(MouseEvent.MOUSE_MOVE, handleFullScreenMouseMove);
					
					fullScreenAnimate = new Animate();
					fullScreenAnimate.motionPaths = Vector.<MotionPath>([
						 new SimpleMotionPath("alpha", "1", "0")
					]);
					
					fullScreenTimer = new Timer(2500, 1);
					fullScreenTimer.addEventListener(TimerEvent.TIMER_COMPLETE, handleFullScreenTimerComplete);					
					fullScreenTimer.start();
					if (isDesktop) {
						normalScreenRect = new Rectangle(0, 0, width, height);
					}
				}
				else if(fullScreenTimer) {
					if (fullScreenTimer.running) {
						fullScreenTimer.stop();
					}
					systemManager.getSandboxRoot().removeEventListener(MouseEvent.MOUSE_MOVE, handleFullScreenMouseMove);
					
					fullScreenTimer.removeEventListener(TimerEvent.TIMER_COMPLETE, handleFullScreenTimerComplete);
					fullScreenTimer = null;
					fullScreenAnimate = null;
					if (isDesktop) {
						height = normalScreenRect.height;
						width  = normalScreenRect.width;
						setTimeout(function():void {
							setStyle("height", "100%");
							setStyle("width", "100%");
						}, 10);
					}
				}
			}
			
			/**
			 * Handle fullScreen mouse move
			 */ 
			private function handleFullScreenMouseMove(event:MouseEvent):void
			{
				if (fullScreenTimer.running) {
					fullScreenTimer.stop();
				}
				if (fullScreenAnimate.isPlaying) {
					fullScreenAnimate.stop();
				}
				
				playback.alpha = 1;
				menu.alpha     = 1;

				playback.visible = true;
				menu.visible 	 = true;
				fullScreenTimer.start();
				if (windows) {
					for (var i:int = windows.length; i--;) {
						windows[i].show();
						windows.splice(i, 1);
					}
					windows = null;
				}
			}
			
			/**
			 * Handle fullscreen timer complete
			 * 
			 * @param event
			 */ 
			private function handleFullScreenTimerComplete(event:TimerEvent):void
			{
				fullScreenAnimate.play([menu, playback]);
				
				if (menu.hideWindowsInFullScreen) {
					windows = new Vector.<BaseWindow>();
					
					for each(var chartsWindow:ChartsWindow in chartsWindows.windows) {
						chartsWindow.hide();
						windows.push(chartsWindow);
					}
					
					if (topologyWindows.localTopologyWindow.visible) {
						topologyWindows.localTopologyWindow.hide();
						windows.push(topologyWindows.localTopologyWindow);
					}
					
					if (topologyWindows.globalTopologyWindow.visible) {
						topologyWindows.globalTopologyWindow.hide();
						windows.push(topologyWindows.globalTopologyWindow);
					}
					
					if (sequencesWindow.visible) {
						sequencesWindow.hide();
						windows.push(sequencesWindow);
					}
					
					if (nodeWindows.window.visible) {
						nodeWindows.window.hide();
						windows.push(nodeWindows.window);
					}
					
					if (nodeWindows.window2.visible) {
						nodeWindows.window2.hide();
						windows.push(nodeWindows.window2);
					}
				}
				
				
			}
			
			//--------------------------------------------------------------------------
			//
			//  Menu Buttons Events
			//
			//--------------------------------------------------------------------------
			
			/**
			 * Handle menu stats button click
			 * 
			 * @param event
			 */ 
			private function handleMenuStatsButtonClick(event:Event):void
			{
				chartsWindows.add();
			}
			
			/**
			 * Handle menu sequences button click
			 * 
			 * @param event
			 */ 
			private function handleMenuSequencesButtonClick(event:Event):void
			{
				sequencesWindow.toggle();
			}
			
			/**
			 * Handle menu local topology button click
			 * 
			 * @param event
			 */ 
			private function handleMenuLocalTopologyButtonClick(event:Event):void
			{
				topologyWindows.localTopologyWindow.toggle();
			}
			
			/**
			 * Handle menu topology button click
			 * 
			 * @param event
			 */ 
			private function handleMenuTopologyButtonClick(event:Event):void
			{
				// toggle topology window
				topologyWindows.globalTopologyWindow.toggle();
			}
			
		]]>
	</fx:Script>
	<view:VisualizationView id="visualizerView"  />
	
	<!-- Top Menu -->
	<menus:Menu id="menu" top="0" width="100%" height="60" />	

	<!-- Playback Controller -->
	<menus:Playback id="playback" bottom="0" width="100%" height="50" />
</s:Group>
